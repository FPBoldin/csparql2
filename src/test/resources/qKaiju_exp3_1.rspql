PREFIX tr: <http://polimi.deib/tracing#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

REGISTER RSTREAM <s1> AS
SELECT (?cid AS ?CustomerId) (SUM(?tCalc) AS ?timeCPURouteCalcperCustomerId)
FROM NAMED WINDOW <win> ON <jsonTraces> [RANGE PT10S STEP PT10S]
WHERE  {
    WINDOW ?w {
        ?t a tr:Trace .
        ?s a tr:Span ;
            tr:spanOfTrace ?t ;
            tr:operationName "HTTP GET /customer"^^xsd:string ;
            tr:hasLog ?log .
        ?log tr:hasField ?f .
        ?f tr:tagKey "customer_id"^^xsd:string ;
            tr:stringVal ?cid.
        ?s2 a tr:Span ;
            tr:spanOfTrace ?t ;
            tr:operationName "HTTP GET /route"^^xsd:string ;
            tr:hasLog ?log2 .
        ?log2 tr:hasField ?fR ;
            tr:hasField ?fT .
        ?fR tr:stringVal "RouteCalc"^^xsd:string .      
        ?fT tr:tagKey "time"^^xsd:string ;
            tr:doubleVal ?tCalc .   
    }
} 
GROUP BY ?cid